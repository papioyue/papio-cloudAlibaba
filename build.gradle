plugins {
    id 'java'
    id 'org.springframework.boot' version '2.5.3'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id "com.bmuschko.docker-spring-boot-application" version "6.6.0"
}

repositories {
    mavenCentral()
}

group 'org.papio'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8


subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'eclipse'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'jacoco'

    group 'org.papio'
    version '1.0-SNAPSHOT'

    sourceCompatibility = 1.8

    ext {
        set('springBootVersion', "2.5.3")
        set('springBootAdminVersion', "2.4.3")
        set('springCloudVersion', "Hoxton.SR12")
        set('springCloudAlibabaVersion', "2021.1")
        set('springFrameworkVersion', "5.3.9")
        set('springSecurityVersion', "5.5.1")
        set('springOauth2Version', "2.5.1.RELEASE")
        set('junitJupiterVersion', "5.5.2")
        set('junitPlatformLauncherVersion', "1.5.2")

        set('commonsLangVersion', "3.12.0")
        set('lombokVersion', "1.18.20")
        set('jacksonVersion', "2.12.4")
        set('xstreamVersion', "1.4.17")
        set('slf4jVersion', "2.0.0-alpha2")
        set('yamlVersion', "1.29")
        set('aspectjVersion', "1.9.8.M1")
        set('redissonVersion', "3.16.1")
        set('druidVersion', "1.2.6")
        set('mybatisPlusVersion', "3.4.3.1")
        set('generatorVersion', "3.5.0")
        set('velocityEngineVersion', "2.3")
        set('pagehelperSpringVersion', "1.3.1")
        set('pagehelperVersion', "5.2.1")
        set('swagger2Version', "3.0.0")
        set('knife4jVersion', "3.0.3")
        set('servletVersion', "4.0.1")
        set('sentinelVersion', "1.8.2")
        set('feignVersion', "11.6")
        set('ribbonVersion', "2.7.18")
        set('httpClientVersion', "4.5.13")
    }

    repositories {
        maven { url "https://maven.aliyun.com/repository/central" }
        maven { url "https://repo.spring.io/milestone" }
        maven { url "https://plugins.gradle.org/m2/" }
        mavenCentral()
        jcenter()
    }

    configurations {
        developmentOnly
        runtimeClasspath {
            extendsFrom developmentOnly
        }
        compileOnly {
            extendsFrom annotationProcessor
        }

        // 排除 tomcat 依赖（使用 undertow）
        implementation.exclude group: 'org.springframework.boot', module: 'spring-boot-starter-tomcat'
        // 排除 logback 依赖（使用 log4j2）
        implementation.exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
    }

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-undertow'

        implementation 'org.springframework.boot:spring-boot-starter-log4j2'
        implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml'  // 添加 yml 依赖（用于识别 log4j2.yml 文件）

        implementation 'org.springframework.boot:spring-boot-starter-actuator'

        compileOnly "org.projectlombok:lombok:${lombokVersion}"
        annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

        testImplementation('org.springframework.boot:spring-boot-starter-test') {
            exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
        }

        testImplementation "org.junit.jupiter:junit-jupiter-engine:${junitJupiterVersion}"
        testImplementation "org.junit.jupiter:junit-jupiter-params:${junitJupiterVersion}"
        testImplementation "org.junit.platform:junit-platform-launcher:${junitPlatformLauncherVersion}"
    }

    dependencyManagement {
        imports {
            mavenBom "de.codecentric:spring-boot-admin-dependencies:${springBootAdminVersion}"
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
            mavenBom "com.alibaba.cloud:spring-cloud-alibaba-dependencies:${springCloudAlibabaVersion}"
        }
    }

    test {
        useJUnitPlatform()
        ignoreFailures = true
    }

    /**
     * 添加jacoco测试覆盖率
     */
    jacocoTestReport {
        reports {
            xml.enabled true
            csv.enabled true
            html.enabled true

            xml.destination file("${buildDir}/reports/jacoco/jacocoReport/jacocoXml.xml")
            csv.destination file("${buildDir}/reports/jacoco/jacocoReport/jacocoCsv.csv")
            html.destination file("${buildDir}/reports/jacoco/jacocoReport/jacocoHtml")
        }
    }
    tasks.check.dependsOn jacocoTestReport

    /**
     * clean 命令时删除 logs 目录
     */
    task cleanOutDir(type: Delete) {
        delete "${projectDir}/logs"
        delete "${rootDir}/logs"
    }
    tasks.clean.dependsOn(cleanOutDir)

    /**
     * 根据环境打包默认dev开发环境，命令：./gradlew -x test clean bootJar -Denv=dev
     */
    def env = System.getProperty("env") ?: "dev"
    /*processResources {
        filter ReplaceTokens, tokens: [env: env]
    }*/

}